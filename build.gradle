buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.ajoberstar.grgit:grgit-gradle:3.0.0'
    }
}

plugins {
    // id 'com.matthewprenger.cursegradle' version '1.1.2'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'maven-publish'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "1.80pr1.14"
group = "org.squiddev"
archivesBaseName = "cc-tweaked"

minecraft {
    runs {
        client = {
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        server = {
            workingDirectory project.file('run').canonicalPath
        }
    }

    mappings channel: 'snapshot', version: '20190118-1.13.1'
    // makeObfSourceJar = false

    accessTransformer file('src/main/resources/META-INF/computercraft_at.cfg')
}

repositories {
    maven {
        name = "JEI"
        url  = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "squiddev"
        url = "https://dl.bintray.com/squiddev/maven"
    }

    ivy { artifactPattern "https://asie.pl/files/mods/Charset/LibOnly/[module]-[revision](-[classifier]).[ext]" }
}

configurations {
    shade
    compile.extendsFrom shade
    deployerJars
}

dependencies {
    minecraft 'net.minecraftforge.test:forge:1.13-24.0.146-1.13-pre'

    // deobfProvided "mezz.jei:jei_1.12.2:4.8.5.159:api"
    // deobfProvided "pl.asie:Charset-Lib:0.5.4.6"

    shade 'org.squiddev:Cobalt:0.4.0'

    testCompile 'junit:junit:4.11'

    deployerJars "org.apache.maven.wagon:wagon-ssh:3.0.0"
}

sourceSets {
    main {
        java {
            exclude 'dan200/computercraft/shared/integration'
        }
    }
}

javadoc {
    include "dan200/computercraft/api/**/*.java"
}

jar {
    dependsOn javadoc

    manifest {
        attributes('FMLAT': 'computercraft_at.cfg')
    }

    into("docs", { from (javadoc.destinationDir) })

    into("api", { from (sourceSets.main.allSource) {
        include "dan200/computercraft/api/**/*.java"
    }})

    from configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }
}

import org.ajoberstar.grgit.Grgit

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", '1.13'

    def hash = 'none'
    Set<String> contributors = []
    try {
        def grgit = Grgit.open(dir: '.')
        hash = grgit.head().id

        def blacklist = ['GitHub', 'dan200', 'Daniel Ratcliffe']
        grgit.log().each {
            if (!blacklist.contains(it.author.name)) contributors.add(it.author.name)
            if (!blacklist.contains(it.committer.name)) contributors.add(it.committer.name)
        }
    } catch(Exception ignored) { }

    inputs.property "commithash", hash

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        include 'data/computercraft/lua/rom/help/credits.txt'

        expand 'version':project.version,
               'mcversion':'1.13',
               'gitcontributors':contributors.sort(false, String.CASE_INSENSITIVE_ORDER).join('\n')
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
        exclude 'data/computercraft/lua/rom/help/credits.txt'
    }
}

/*
curseforge {
    apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''
    project {
        id = '282001'
        releaseType = 'beta'
        changelog = "Release notes can be found on the GitHub repository (https://github.com/SquidDev-CC/CC-Tweaked/releases/tag/v${project.version})."
    }
}
*/

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            // artifact sourceJar
        }
    }
}

uploadArchives {
    repositories {
        if(project.hasProperty('mavenUploadUrl')) {
            mavenDeployer {
                configuration = configurations.deployerJars

                repository(url: project.property('mavenUploadUrl')) {
                    authentication(
                        userName: project.property('mavenUploadUser'),
                        privateKey: project.property('mavenUploadKey'))
                }

                pom.project {
                    name 'CC: Tweaked'
                    packaging 'jar'
                    description 'A fork of ComputerCraft which aims to provide earlier access to the more experimental and in-development features of the mod.'
                    url 'https://github.com/SquidDev-CC/CC-Tweaked'

                    scm {
                        url 'https://github.com/dan200/ComputerCraft.git'
                    }

                    issueManagement {
                        system 'github'
                        url 'https://github.com/dan200/ComputerCraft/issues'
                    }

                    licenses {
                        license {
                            name 'ComputerCraft Public License, Version 1.0'
                            url 'https://github.com/dan200/ComputerCraft/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }
                }

                pom.whenConfigured { pom ->
                    pom.dependencies.clear()
                }
            }
        }
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint"
    }
}

// runClient.outputs.upToDateWhen { false }
// runServer.outputs.upToDateWhen { false }
